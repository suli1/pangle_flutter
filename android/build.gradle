group 'io.github.nullptrx.pangleflutter'
version '1.3.0'

buildscript {
    ext.kotlin_version = '1.5.21'
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://artifact.bytedance.com/repository/pangle"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


def getCurrentProjectDir() {
    String result = ""
    rootProject.allprojects { project ->

        if (project.toString().contains("pangle_flutter")) {
            if (project.properties.get("identityPath").toString() == ":pangle_flutter") {
                result = project.properties.get("projectDir").toString()
            }
        }
    }
    return result
}

rootProject.allprojects {
    def dir = getCurrentProjectDir()
    repositories {
        maven {
            url "$dir/aar"
        }
        google()
        mavenCentral()
        maven {
            url "https://artifact.bytedance.com/repository/pangle"
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}
def kEnv = localProperties.getProperty('pangle_flutter.env')
if (kEnv == null) {
    kEnv = 'cn'
}

android {
    compileSdkVersion 29

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.manifest.srcFile "src/main/AndroidManifest-${kEnv}.xml"
        switch (kEnv) {
            case 'cn':
                main.java.srcDirs += 'src/main/cn'
                break
        }
    }

    defaultConfig {
        minSdkVersion 16
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    buildTypes {
        release {
            minifyEnabled true
            consumerProguardFiles "proguard-rules.pro"
        }
    }
}


dependencies {
    implementation 'androidx.appcompat:appcompat:+'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    compileOnly 'androidx.annotation:annotation:1.1.0'
    implementation "com.ad.pangle:sdk:3.9.0"
    implementation "com.ad.tencent:sdk:4.391.1261"
    implementation "com.gromore.cn:gromore-sdk:2.9.2.0"

    //GroMore_sdk for only test
    //implementation "com.gromore.cn:gromore-test-tools:2.9.2.0"

    //GroMore_sdk adapter
    implementation "com.gromore.cn:gdt-adapter:4.391.1261.1"

}
